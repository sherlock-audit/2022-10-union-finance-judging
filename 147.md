GimelSec

medium

# AssetManager.removeToken() should check whether getPoolBalance() == 0

## Summary

`AssetManager.removeToken(token)` would set `supportedMarkets[token]` to false. It causes assets to be frozen, if there are tokens that haven't been withdrawn from money markets. 

## Vulnerability Detail

`AssetManager.removeToken()` is a simple method to disable a market-supported token in `AssetManager`. It would set `supportedMarkets[token]` to false. Thus, `AssetManager.isMarketSupported(token)` will return false.

```solidity
    function removeToken(address tokenAddress) external override onlyAdmin {
        bool isExist = false;
        uint256 index;
        uint256 supportedTokensLength = supportedTokensList.length;

        for (uint256 i = 0; i < supportedTokensLength; i++) {
            if (tokenAddress == address(supportedTokensList[i])) {
                isExist = true;
                index = i;
                break;
            }
        }

        if (isExist) {
            supportedTokensList[index] = supportedTokensList[supportedTokensLength - 1];
            supportedTokensList.pop();
            supportedMarkets[tokenAddress] = false;
        }
    }
```

```solidity
    function isMarketSupported(address tokenAddress) public view override returns (bool) {
        return supportedMarkets[tokenAddress];
    }
```

Once a token is not market-supported, UToken or Usermanager can only withdraw the tokens from AssetManager itself in `AssetManager.withdraw()`. It won’t try to withdraw the tokens from money markets. 

```solidity
    function withdraw(
        address token,
        address account,
        uint256 amount
    ) external override whenNotPaused nonReentrant onlyAuth(token) returns (bool) {
        if (!_checkSenderBalance(msg.sender, token, amount)) revert InsufficientBalance();

        uint256 remaining = amount;

        // If there are tokens in Asset Manager then transfer them on priority
        uint256 selfBalance = IERC20Upgradeable(token).balanceOf(address(this));
        if (selfBalance > 0) {
            uint256 withdrawAmount = selfBalance < remaining ? selfBalance : remaining;
            remaining -= withdrawAmount;
            IERC20Upgradeable(token).safeTransfer(account, withdrawAmount);
        }

        if (isMarketSupported(token)) {
            uint256 withdrawSeqLength = withdrawSeq.length;
            // iterate markets according to defined sequence and withdraw
            for (uint256 i = 0; i < withdrawSeqLength && remaining > 0; i++) {
                IMoneyMarketAdapter moneyMarket = moneyMarkets[withdrawSeq[i]];
                if (!moneyMarket.supportsToken(token)) continue;

                uint256 supply = moneyMarket.getSupply(token);
                if (supply == 0) continue;

                uint256 withdrawAmount = supply < remaining ? supply : remaining;
                remaining -= withdrawAmount;
                moneyMarket.withdraw(token, account, withdrawAmount);
            }
        }

        if (!_isUToken(msg.sender, token)) {
            balances[msg.sender][token] = balances[msg.sender][token] - amount + remaining;
            totalPrincipal[token] = totalPrincipal[token] - amount + remaining;
        }

        emit LogWithdraw(token, account, amount, remaining);

        return true;
    }
```

Therefore, if there are tokens remaining in the money markets. `AssetManager.removeToken()` should not disable that token. Otherwise, the tokens may be stuck in the money markets.

## Impact

`AssetManager.removeToken()` could cause assets to be frozen. A benign admin could accidentally do that. And a malicious admin can use this method to disable `AssetManager.withdraw()`.

It’s a serious problem, but only the admin can trigger it. So we label it medium

## Code Snippet

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L396-L414

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L214

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L345


## Tool used

Manual Review

## Recommendation

Check `AssetManager.getPoolBalance(token) == 0` in `AssetManger.removeToken`

```diff
    function removeToken(address tokenAddress) external override onlyAdmin {
+       require(AssetManager.getPoolBalance(tokenAddress) == 0)
        bool isExist = false;
        uint256 index;
        uint256 supportedTokensLength = supportedTokensList.length;

        for (uint256 i = 0; i < supportedTokensLength; i++) {
            if (tokenAddress == address(supportedTokensList[i])) {
                isExist = true;
                index = i;
                break;
            }
        }

        if (isExist) {
            supportedTokensList[index] = supportedTokensList[supportedTokensLength - 1];
            supportedTokensList.pop();
            supportedMarkets[tokenAddress] = false;
        }
    }
```
