Jeiwan

high

# `AssetManager.withdraw()` can lock user funds indefinitely

## Summary
`AssetManager.withdraw()` can lock user funds indefinitely
## Vulnerability Detail
`AssetManager` is the contract that holds and manages all staked and deposited tokens. The [withdraw](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L328) function can be called (by a `UserManager` or a `UToken`) to withdraw funds from the asset manager. Since the asset manager deposits a portion or the entire amount of tokens into a money market (e.g. Aave), withdrawing can require withdrawing tokens from money markets as well.

The `withdraw` function is called by `UserManager` and `UToken` in different situations:
1. in [UserManager.unstake()](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L703) to withdraw staked tokens;
1. in [UToken.borrow()](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/UToken.sol#L512) to borrow staked/deposited funds;
1. in [UToken.redeem()](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/UToken.sol#L707) to convert uTokens to underlying tokens;
1. in [UToken.removeReserves()](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/UToken.sol#L770) to remove reserves from the asset manager.

In all of these situations the withdraw functions is called with a certain amount and it's always expected that the call will withdraw *the entire amount* (notice that in each of these functions `amount` is always subtracted from a state variable before `withdraw` is called). However, the withdraw function can in fact withdraw only a portion of the requested amount. This can happen when:
1.  `AssetManager` holds [less than the amount requested](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L337-L343);
1.  and:
    1. [the money market is no longer supported](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L345);
    1. or, [the money market has 0 supply](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L353);
    1. or, [the money market has less than the amount requested](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L355-L357).
## Impact
When a partial amount is withdrawn, the contract that called the `withdraw` function will subtract the entire amount. As a result, users will have a portion of their funds locked in `AssetManager` or one of the money markets. They won't be able to withdraw these funds since they'll already have been counted as withdrawn funds.
## Code Snippet
```solidity
function withdraw(
    address token,
    address account,
    uint256 amount // @audit the amount requested for withdrawal
) external override whenNotPaused nonReentrant onlyAuth(token) returns (bool) {
    if (!_checkSenderBalance(msg.sender, token, amount)) revert InsufficientBalance();

    uint256 remaining = amount;

    // If there are tokens in Asset Manager then transfer them on priority
    uint256 selfBalance = IERC20Upgradeable(token).balanceOf(address(this));
    if (selfBalance > 0) {
        uint256 withdrawAmount = selfBalance < remaining ? selfBalance : remaining;
        remaining -= withdrawAmount; // @audit only a portion of the requested amount is withdrawn
        IERC20Upgradeable(token).safeTransfer(account, withdrawAmount);
    }

    if (isMarketSupported(token)) {
        uint256 withdrawSeqLength = withdrawSeq.length;
        // iterate markets according to defined sequence and withdraw
        for (uint256 i = 0; i < withdrawSeqLength && remaining > 0; i++) {
            IMoneyMarketAdapter moneyMarket = moneyMarkets[withdrawSeq[i]];
            if (!moneyMarket.supportsToken(token)) continue;

            uint256 supply = moneyMarket.getSupply(token);
            if (supply == 0) continue;

            uint256 withdrawAmount = supply < remaining ? supply : remaining;
            remaining -= withdrawAmount; // @audit only a portion of the requested amount is withdrawn
            moneyMarket.withdraw(token, account, withdrawAmount);
        }
    }

    if (!_isUToken(msg.sender, token)) {
        balances[msg.sender][token] = balances[msg.sender][token] - amount + remaining;
        totalPrincipal[token] = totalPrincipal[token] - amount + remaining;
    }

    emit LogWithdraw(token, account, amount, remaining);

    return true; // @audit a boolean is returned, not the actual withdrawn amount
}
```
## Tool used
Manual Review
## Recommendation
In the `withdraw` function, return the amount that was actually withdrawn. In all the functions that call `AssetManager.withdraw()`, check the amount returned by the call and update state accordingly.