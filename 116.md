TurnipBoy

medium

# Staker can pay off interest of delinquent debtor and back claim reward tokens for the entire time the loan was delinquent

## Summary

Reward tokens are not distributed to stakers for the portion of their stake that it held by delinquent loans. A staker can pay off the debt of a delinquent loan to bring it current and claim reward tokens for the full amount as if the debt had never been delinquent. 

## Vulnerability Detail

    function _getUserInfo(
        IUserManager userManager,
        address account,
        address token,
        uint256 futureBlocks
    )
        internal
        returns (
            UserManagerAccountState memory,
            Info memory,
            uint256
        )
    {
        Info memory userInfo = users[account][token];
        uint256 lastUpdatedBlock = userInfo.updatedBlock;
        if (block.number < lastUpdatedBlock) {
            lastUpdatedBlock = block.number;
        }

        uint256 pastBlocks = block.number - lastUpdatedBlock + futureBlocks;

        UserManagerAccountState memory userManagerAccountState;
        (userManagerAccountState.totalFrozen, userManagerAccountState.pastBlocksFrozenCoinAge) = userManager
            .updateFrozenInfo(account, pastBlocks);

        return (userManagerAccountState, userInfo, pastBlocks);
    }

`Comtroler.sol#_getUserInfo` updates the amount of stake frozen for an account when it calls `userManager.updateFrozenInfo`.

    function updateFrozenInfo(address staker, uint256 pastBlocks) external onlyComptroller returns (uint256, uint256) {
        return _updateFrozen(staker, pastBlocks);
    }

    function _updateFrozen(address staker, uint256 pastBlocks) internal returns (uint256, uint256) {
        (uint256 memberTotalFrozen, uint256 memberFrozenCoinAge) = getFrozenInfo(staker, pastBlocks);

        uint256 memberFrozenBefore = memberFrozen[staker];
        if (memberFrozenBefore != memberTotalFrozen) {
            memberFrozen[staker] = memberTotalFrozen;
            totalFrozen = totalFrozen - memberFrozenBefore + memberTotalFrozen;
        }

        return (memberTotalFrozen, memberFrozenCoinAge);
    }

`UserManger.sol#_updateFrozen` loops through all current debtors and evaluates if they are overdue. The sum of all overdue debts are considered frozen. `_getUserInfo` is only called when `Comptroller.sol#withdrawRewards` is called, which is only called when a user stakes/unstakes or manually calls `UserManager.sol#withdrawRewards`. If the user avoids those actions then comptroller will never checkpoint the user with any frozen balance. 

        if (repayAmount >= interest) {

            bool isOverdue = checkIsOverdue(borrower);

            toReserveAmount = (interest * reserveFactorMantissa) / WAD;
            toRedeemableAmount = interest - toReserveAmount;

            totalBorrows -= (repayAmount - interest);

            accountBorrows[borrower].principal = borrowedAmount - repayAmount;
            accountBorrows[borrower].interest = 0;

            if (getBorrowed(borrower) == 0) {
                accountBorrows[borrower].lastRepay = 0;
            } else {
                accountBorrows[borrower].lastRepay = getBlockNumber();
            }

Inside `UToken.sol#_repayBorrowFresh` if `repayAmount >= interest` then `accountBorrows[borrower].lastRepay` is updated to the current block number. A staker can abuse this to back claim rewards for loans that were delinquent as long as they never call any functions in `UserManger.sol` that updates their frozen stake. The staker would repay enough of the users loan to cover the accrued interest, updating `lastRepay` to the current block. After they can withdraw their rewards as if the debt had never been delinquent.

## Impact

Staker is given unfair claim to more reward tokens than they should

## Code Snippet

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/UToken.sol#L573-L646

## Tool used

Manual Review

## Recommendation

In `UToken.sol#_repayBorrowFresh` if `isOverdue` is true it should checkpoint all stakers from which the borrower is currently borrowing from.