hyh

high

# It is possible to become a member without creating any ability to borrow

## Summary

UserManager's registerMember() only requires the existence of a `effectiveCount` vouches from stakers with positive `stakedAmount`. However, trust for that particular borrower who is to become new member, can be zero. I.e. the existence of enough vouches doesn't guarantee that `newMember` can actually borrow anything, which can be used to become a nominal member, receiving extra rewards without any participation of the borrower in the system and vouchers taking the credit risk for this borrower.

## Vulnerability Detail

Bob wants to become a member. He knows Alice and Mike, who are Union members with active stakes and trust. The problem is that Alice and Mike don't really want to trust Bob anything. They can do him a favor without taking his risk by calling `updateTrust(Bob, trustAmount = 0)` as updateTrust() allows for zero amount both for new and existing vouches.

In the same time registerMember() only checks that `staker.stakedAmount > 0`, which is true as both Alice and Mike. This condition doesn't imply that Bob can borrow anything, still counting as if it does.

This way they let Bob in, not creating any ability for him to use the credit. Bob can utilize his status to farm UNION tokens with member `1.0` multiplier, gaining additional `0.25` multiplier for his staked amount.

## Impact

Bob has his rewards inflated at the expense of all UNION token holders, effectively stealing from them via additional token issuance.

The exploit allows Bob to do that without Alice and Mike bearing any additional risk, while all other honest staker do bear that risk for their vouchees, which is the material basis of them receiving additional `0.25` multiplier for the rewards.

Given that this is direct violation of the design of the system, monetary loss for all holders from bloated UNION issuance and no additional conditions required to execute the attack, setting severity to be **high**.

## Code Snippet

registerMember() requires existence of `vouchers` array for Bob the new member with enough stakers listed there having positive stakes:

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L626-L650

```solidity
    /**
     *  @notice Register a a member, and burn an application fees
     *  @dev    In order to register as a member an address must be recieving x amount
     *          of vouches greater than 0 from stakers. x is defined by `effectiveCount`
     *          Emits {LogRegisterMember} event
     *  @param newMember New member address
     */
    function registerMember(address newMember) public virtual whenNotPaused {
        if (stakers[newMember].isMember) revert NoExistingMember();

        uint256 count = 0;
        uint256 vouchersLength = vouchers[newMember].length;

        // Loop through all the vouchers to count how many active vouches there
        // are that are greater than 0. Vouch is the min of stake and trust
        for (uint256 i = 0; i < vouchersLength; i++) {
            Vouch memory vouch = vouchers[newMember][i];
            Staker memory staker = stakers[vouch.staker];
            if (staker.stakedAmount > 0) count++;
            if (count >= effectiveCount) break;
        }

        if (count < effectiveCount) revert NotEnoughStakers();

        stakers[newMember].isMember = true;
```

In the same time this doesn't guarantee that any positive credit line was created for Bob as his trust can be just set to zero (so the positive stakes being checked has nothing to do with him).

updateTrust() allows for zero `trustAmount` for any new or non utilized vouch:

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L525-L555

```solidity
    function updateTrust(address borrower, uint96 trustAmount) external onlyMember(msg.sender) whenNotPaused {
        address staker = msg.sender;
        if (borrower == staker) revert ErrorSelfVouching();
        if (!checkIsMember(staker)) revert AuthFailed();

        // Check if this staker is already vouching for this borrower
        // If they are already vouching then update the existing vouch record
        // If this is a new vouch then insert a new Vouch record
        Index memory index = voucherIndexes[borrower][staker];
        if (index.isSet) {
            // Update existing record checking that the new trust amount is
            // not less than the amount of stake currently locked by the borrower
            Vouch storage vouch = vouchers[borrower][index.idx];
            if (trustAmount < vouch.locked) revert TrustAmountLtLocked();
            vouch.trust = trustAmount;
        } else {
            // If the member is overdue they cannot create new vouches they can
            // only update existing vouches
            if (uToken.checkIsOverdue(staker)) revert VouchWhenOverdue();

            // This is a new vouch so we need to check that the
            // member has not reached the max voucher limit
            uint256 voucheesLength = vouchees[staker].length;
            if (voucheesLength >= maxVouchers) revert MaxVouchees();

            // Get the new index that this vouch is going to be inserted at
            // Then update the voucher indexes for this borrower as well as
            // Adding the Vouch the the vouchers array for this staker
            uint256 voucherIndex = vouchers[borrower].length;
            voucherIndexes[borrower][staker] = Index(true, uint128(voucherIndex));
            vouchers[borrower].push(Vouch(staker, trustAmount, 0, 0));
```

This means that fake vouches can be created, which provides a way to manipulate any further conditions tied to their existence.

Particularly, registerMember() stated goal is not met with the positive stake requirement:

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L628-L629

```solidity
     *  @dev    In order to register as a member an address must be recieving x amount
     *          of vouches greater than 0 from stakers. x is defined by `effectiveCount`
```

Total Bob, Alice and Mike expenses are gas costs, and no additional risk was taken.

Bob will use his status just to farm UNION with better multiplier `memberRatio > nonMemberRatio`:

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/token/Comptroller.sol#L523-L543

```solidity
    function _getRewardsMultiplier(
        uint256 userStaked,
        uint256 lockedStake,
        uint256 totalFrozen_,
        bool isMember_
    ) internal pure returns (uint256) {
        if (isMember_) {
            if (userStaked == 0 || totalFrozen_ >= lockedStake) {
                return memberRatio;
            }

            uint256 effectiveLockedAmount = lockedStake - totalFrozen_;
            uint256 effectiveStakeAmount = userStaked - totalFrozen_;

            uint256 lendingRatio = effectiveLockedAmount.wadDiv(effectiveStakeAmount);

            return lendingRatio + memberRatio;
        } else {
            return nonMemberRatio;
        }
    }
```

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/token/Comptroller.sol#L56-L64

```solidity
    /**
     * @dev Non member reward multiplier rate (75%)
     */
    uint256 public constant nonMemberRatio = 75 * 10**16; // 75%;

    /**
     * @dev Member reward multiplier rate (100%)
     */
    uint256 public constant memberRatio = 10**18;
```

## Tool used

Manual Review

## Recommendation

Consider requiring `trustAmount` argument of updateTrust() to be positive or even greater than some dust threshold `amountDustThreshold`, leaving cancelVouch() only to be used for trust removal, for example:

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L525-L548

```solidity
    function updateTrust(address borrower, uint96 trustAmount) external onlyMember(msg.sender) whenNotPaused {
+	if (trustAmount < amountDustThreshold) revert AmountZero();  // cancelVouch() to be used instead
        address staker = msg.sender;
        if (borrower == staker) revert ErrorSelfVouching();
        if (!checkIsMember(staker)) revert AuthFailed();

        // Check if this staker is already vouching for this borrower
        // If they are already vouching then update the existing vouch record
        // If this is a new vouch then insert a new Vouch record
        Index memory index = voucherIndexes[borrower][staker];
        if (index.isSet) {
            // Update existing record checking that the new trust amount is
            // not less than the amount of stake currently locked by the borrower
            Vouch storage vouch = vouchers[borrower][index.idx];
            if (trustAmount < vouch.locked) revert TrustAmountLtLocked();
            vouch.trust = trustAmount;
        } else {
            // If the member is overdue they cannot create new vouches they can
            // only update existing vouches
            if (uToken.checkIsOverdue(staker)) revert VouchWhenOverdue();

            // This is a new vouch so we need to check that the
            // member has not reached the max voucher limit
            uint256 voucheesLength = vouchees[staker].length;
            if (voucheesLength >= maxVouchers) revert MaxVouchees();
```
