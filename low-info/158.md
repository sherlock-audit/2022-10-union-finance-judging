GimelSec

medium

# interestRatePerBlock in FixedInterestRateModel.constructor can exceed BORROW_RATE_MAX_MANTISSA

## Summary

`interestRatePerBlock` should not exceed `BORROW_RATE_MAX_MANTISSA`. `FixedInterestRateModel.setInterestRate` has the correct check. But `FixedInterestRateModel.constructor` doesn’t.

## Vulnerability Detail

According to `FixedInterestRateModel.setInterestRate`. `interestRatePerBlock` should not exceed `BORROW_RATE_MAX_MANTISSA`.

```solidity
    function setInterestRate(uint256 _interestRatePerBlock) external override onlyOwner {
        if (_interestRatePerBlock > BORROW_RATE_MAX_MANTISSA) revert BorrowRateExceeded();
        interestRatePerBlock = _interestRatePerBlock;

        emit LogNewInterestParams(_interestRatePerBlock);
    }
```

However, the constructor doesn’t have the same check.

```solidity
    constructor(uint256 interestRatePerBlock_) {
        interestRatePerBlock = interestRatePerBlock_;

        emit LogNewInterestParams(interestRatePerBlock_);
    }
```

It seems to be a low-severity issue. But according to this [report](https://code4rena.com/reports/2021-10-union/#m-04-change-in-interest-rate-can-disable-repay-of-loan), a malicious admin could use this issue to temporarily disable repay of loan in `UToken`.

When doing repay in `UToken`, `UToken.borrowRatePerBlock` would be called. if `borrowRateMantissa > BORROW_RATE_MAX_MANTISSA`, the repay always revert.

```solidity
    function borrowRatePerBlock() public view override returns (uint256) {
        uint256 borrowRateMantissa = interestRateModel.getBorrowRate();
        if (borrowRateMantissa > BORROW_RATE_MAX_MANTISSA) revert BorrowRateExceedLimit();

        return borrowRateMantissa;
    }
```

In this [report](https://code4rena.com/reports/2021-10-union/#m-04-change-in-interest-rate-can-disable-repay-of-loan), a malicious admin use `FixedInterestRateModel.setInterestRate` to set a bad `interestRatePerBlock`. The issue has been resolved. But if the constructor doesn’t check `interestRatePerBlock`. A malicious admin can create a new `FixedInterestRateModel`  contract with bad `interestRatePerBlock`. Then he or she can use `UToken.setInterestRateModel` to set the bad interestRateModel. Therefore, a malicious admin can temporarily disable repay of loan in `UToken` by setting a bad `FixedInterestRateModel`.

```solidity
    function setInterestRateModel(address newInterestRateModel_) external override onlyAdmin {
        address oldInterestRateModel = address(interestRateModel);
        address newInterestRateModel = newInterestRateModel_;
        interestRateModel = IInterestRateModel(newInterestRateModel);
        emit LogNewMarketInterestRateModel(oldInterestRateModel, newInterestRateModel);
    }
```

## Impact

A malicious admin can leverage this issue to temporarily disable repay of loan in `UToken`.

It’s a serious issue, but only the admin can trigger it. So we label it medium.

## Code Snippet

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/FixedInterestRateModel.sol#L47

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/FixedInterestRateModel.sol#L84-L89

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/UToken.sol#L434-L439

https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/UToken.sol#L334-L339


## Tool used

Manual Review

## Recommendation

Add a check in `FixedInterestRateModel.constructor`
```diff
    constructor(uint256 interestRatePerBlock_) {
+       if (interestRatePerBlock_ > BORROW_RATE_MAX_MANTISSA) revert BorrowRateExceeded();
        interestRatePerBlock = interestRatePerBlock_;
        
        emit LogNewInterestParams(interestRatePerBlock_);
    }
```
