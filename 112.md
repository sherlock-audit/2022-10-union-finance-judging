dipp

high

# Insufficient fee-on-transfer/deflationay tokens support

## Summary

Insufficient support for fee-on-transfer/deflationay tokens in ```UserManager.sol``` and ```UToken.sol``` could lead to core functions not working or may lead to a loss of funds.

## Vulnerability Detail

Fee-on-transfer tokens are tokens that charge a fee for every transfer such that the amount given as input to the transfer function is not the actual amount received by the receiving address. When the ```stake``` function in ```UserManager.sol``` is called, the fee-on-transfer tokens are sent to the UserManager but it receives less than ```amount``` of the tokens. UserManager will then approve the AssetManager for ```amount``` of tokens, instead of the actual amount received, and call its deposit function. If the token is supported, AssetManager will attempt to deposit ```amount``` of tokens to a moneyMarket or it will send the tokens from the UserManager directly to itself. If UserManager's total balance for the fee-on-transfer tokens is less than amount then the transaction reverts and the stake function cannot be called. If the UserManager has >= ```amount``` of the tokens, then the ```amount``` of tokens will be sent through AssetManager, increasing the user's stakedAmount by ```amount``` instead of the actual amount received, leading to a loss of funds for the protocol.

Similarly, when repaying a borrow in ```UToken.sol``` the amount used to deposit to the AssetManager would be more than the actual amount received leading to the borrower not being able to repay until UToken has enough tokens or the repay succeeds and the borrower is credited with more tokens than what UToken actually received.

## Impact

Users would be unable to deposit/stake or, if the UToken.sol and UserManager.sol contracts have more than amount balance, will update state variables for balances incorrectly leading to a loss of funds for the protocol.

## Code Snippet

[UserManager.sol:stake#L664-L682](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L664-L682):
```solidity
    function stake(uint96 amount) public whenNotPaused nonReentrant {
        IERC20Upgradeable erc20Token = IERC20Upgradeable(stakingToken);

        comptroller.withdrawRewards(msg.sender, stakingToken);

        Staker storage staker = stakers[msg.sender];

        if (staker.stakedAmount + amount > maxStakeAmount) revert StakeLimitReached();

        staker.stakedAmount += amount;
        totalStaked += amount;

        erc20Token.safeTransferFrom(msg.sender, address(this), amount);
        erc20Token.safeApprove(assetManager, 0);
        erc20Token.safeApprove(assetManager, amount);

        if (!IAssetManager(assetManager).deposit(stakingToken, amount)) revert AssetManagerDepositFailed();
        emit LogStake(msg.sender, amount);
    }
```

[UToken.sol:_repayBorrowFresh#L642-L643](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/market/UToken.sol#L642-L643);
```solidity
        IERC20Upgradeable(underlying).safeTransferFrom(payer, address(this), repayAmount);
        _depositToAssetManager(repayAmount);
```

[AssetManager.sol#L261-L319](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L261-L319)
```solidity
    function deposit(address token, uint256 amount)
        external
        override
        whenNotPaused
        onlyAuth(token)
        nonReentrant
        returns (bool)
    {
        IERC20Upgradeable poolToken = IERC20Upgradeable(token);
        if (amount == 0) revert AmountZero();

        if (!_isUToken(msg.sender, token)) {
            balances[msg.sender][token] += amount;
            totalPrincipal[token] += amount;
        }

        bool remaining = true;
        if (isMarketSupported(token)) {
            uint256 moneyMarketsLength = moneyMarkets.length;
            // assumption: markets are arranged in order of decreasing liquidity
            // iterate markets till floors are filled
            // floors define minimum amount to maintain confidence in liquidity
            for (uint256 i = 0; i < moneyMarketsLength && remaining; i++) {
                IMoneyMarketAdapter moneyMarket = moneyMarkets[i];

                if (!moneyMarket.supportsToken(token)) continue;
                if (moneyMarket.floorMap(token) <= moneyMarket.getSupply(token)) continue;

                poolToken.safeTransferFrom(msg.sender, address(moneyMarket), amount);
                moneyMarket.deposit(token);
                remaining = false;
            }

            // assumption: less liquid markets provide more yield
            // iterate markets in reverse to optimize for yield
            // do this only if floors are filled i.e. min liquidity satisfied
            // deposit in the market where ceiling is not being exceeded
            for (uint256 j = moneyMarketsLength; j > 0 && remaining; j--) {
                IMoneyMarketAdapter moneyMarket = moneyMarkets[j - 1];
                if (!moneyMarket.supportsToken(token)) continue;

                uint256 supply = moneyMarket.getSupply(token);
                uint256 ceiling = moneyMarket.ceilingMap(token);
                if (supply + amount > ceiling) continue;

                poolToken.safeTransferFrom(msg.sender, address(moneyMarket), amount);
                moneyMarket.deposit(token);
                remaining = false;
            }
        }

        if (remaining) {
            poolToken.safeTransferFrom(msg.sender, address(this), amount);
        }

        emit LogDeposit(token, msg.sender, amount);

        return true;
    }
```

## Tool used

Manual Review

## Recommendation

Check the balance before and after the token transfer and use the difference as the actual amount received instead of the amount given by the user.