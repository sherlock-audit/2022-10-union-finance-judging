Jeiwan

medium

# Removed money market adapters can drain `AssetManager` due to approved token spending

## Summary
Removed money market adapters can drain `AssetManager` due to approved token spending
## Vulnerability Detail
`AssetManager` is the contract that holds and manages all staked and deposited tokens. The contract maintains a list of supported tokens and money market adapters. Money market adapters can be [added](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L420) and [removed](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L440) by the admin. Tokens can also be [added](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L384) and [removed](https://github.com/sherlock-audit/2022-10-union-finance/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L396) by the admin. When a token is added, the maximal amount is approved for spending to all currently supported money market adapters:
```solidity
function addToken(address tokenAddress) external override onlyAdmin {
    if (supportedMarkets[tokenAddress]) revert TokenExists();
    supportedTokensList.push(tokenAddress);
    supportedMarkets[tokenAddress] = true;

    approveAllMarketsMax(tokenAddress);
}

function approveAllMarketsMax(address tokenAddress) public override onlyAdmin {
    IERC20Upgradeable poolToken = IERC20Upgradeable(tokenAddress);
    uint256 moneyMarketsLength = moneyMarkets.length;
    for (uint256 i = 0; i < moneyMarketsLength; i++) {
        poolToken.safeApprove(address(moneyMarkets[i]), 0);
        poolToken.safeApprove(address(moneyMarkets[i]), type(uint256).max);
    }
}
```
Similarly, when a new money market adapter is added, the maximal amount is approved for spending by the adapter:
```solidity
function addAdapter(address adapterAddress) external override onlyAdmin {
    bool isExist = false;
    uint256 moneyMarketsLength = moneyMarkets.length;

    for (uint256 i = 0; i < moneyMarketsLength; i++) {
        if (adapterAddress == address(moneyMarkets[i])) isExist = true;
    }

    if (!isExist) {
        moneyMarkets.push(IMoneyMarketAdapter(adapterAddress));
        withdrawSeq.push(moneyMarkets.length - 1);
    }

    approveAllTokensMax(adapterAddress);
}

function approveAllTokensMax(address adapterAddress) public override onlyAdmin {
    uint256 supportedTokensLength = supportedTokensList.length;
    for (uint256 i = 0; i < supportedTokensLength; i++) {
        IERC20Upgradeable poolToken = IERC20Upgradeable(supportedTokensList[i]);
        poolToken.safeApprove(adapterAddress, 0);
        poolToken.safeApprove(adapterAddress, type(uint256).max);
    }
}
```

However, when a token or a money market adapter is removed, spending approval is not removed:
```solidity
function removeToken(address tokenAddress) external override onlyAdmin {
    bool isExist = false;
    uint256 index;
    uint256 supportedTokensLength = supportedTokensList.length;

    for (uint256 i = 0; i < supportedTokensLength; i++) {
        if (tokenAddress == address(supportedTokensList[i])) {
            isExist = true;
            index = i;
            break;
        }
    }

    if (isExist) {
        supportedTokensList[index] = supportedTokensList[supportedTokensLength - 1];
        supportedTokensList.pop();
        supportedMarkets[tokenAddress] = false;

        // @audit approvals are not removed
    }
}
```
```solidity
function removeAdapter(address adapterAddress) external override onlyAdmin {
    bool isExist = false;
    uint256 index;
    uint256 moneyMarketsLength = moneyMarkets.length;

    for (uint256 i = 0; i < moneyMarketsLength; i++) {
        if (adapterAddress == address(moneyMarkets[i])) {
            isExist = true;
            index = i;
            break;
        }
    }

    if (isExist) {
        moneyMarkets[index] = moneyMarkets[moneyMarketsLength - 1];
        moneyMarkets.pop();

        // @audit approvals are not removed
    }
}
```
## Impact
Unused and abandoned money market adapters can withdraw all Union Protocol users' funds stored in `AssetManager` due to unlimited token spending approvals. 
## Code Snippet
See Vulnerability Detail.
## Tool used
Manual Review
## Recommendation
Consider disapproving spending (setting approved amount to 0) when a token or a money market adapter is removed.